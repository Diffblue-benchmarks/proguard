import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow'
    id 'java'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':base')
    implementation project(':retrace')
    implementation 'org.apache.logging.log4j:log4j-api:2.24.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.2'
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.11.4")
    testImplementation("org.junit.vintage:junit-vintage-engine:5.11.3")
    testImplementation("com.diffblue.cover:cover-annotations:1.3.0")
    testImplementation("org.mockito:mockito-core:4.11.0")
}

task fatJar(type: ShadowJar) {
    destinationDirectory.set(file("$rootDir/lib"))
    archiveFileName.set('proguardgui.jar')
    from sourceSets.main.output
    configurations = [project.configurations.runtimeClasspath]
    manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Main-Class': 'proguard.gui.ProGuardGUI',
                'Multi-Release': true,
                'Implementation-Version': archiveVersion.get())
    }
}

assemble.dependsOn fatJar

afterEvaluate {
    publishing {
        publications.getByName(project.name) {
            pom {
                description = 'ProGuardGUI is an interface for ProGuard, the free shrinker, optimizer, obfuscator, and preverifier for Java bytecode'
            }
        }
    }
}
